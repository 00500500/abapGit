*&---------------------------------------------------------------------*
*&  Include           ZABAPGIT_OBJECT_FUGR
*&---------------------------------------------------------------------*

*----------------------------------------------------------------------*
*       CLASS lcl_object_fugr DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS LCL_OBJECT_FUGR DEFINITION INHERITING FROM LCL_OBJECTS_PROGRAM FINAL.

  PUBLIC SECTION.
    INTERFACES LIF_OBJECT.
    ALIASES MO_FILES FOR LIF_OBJECT~MO_FILES.

  PRIVATE SECTION.
    TYPES: TY_RS38L_INCL_TT TYPE STANDARD TABLE OF RS38L_INCL WITH DEFAULT KEY.

    TYPES: BEGIN OF TY_FUNCTION,
             FUNCNAME          TYPE RS38L_FNAM,
             INCLUDE           TYPE PROGNAME,
             GLOBAL_FLAG       TYPE RS38L-GLOBAL,
             REMOTE_CALL       TYPE RS38L-REMOTE,
             UPDATE_TASK       TYPE RS38L-UTASK,
             SHORT_TEXT        TYPE TFTIT-STEXT,
             REMOTE_BASXML     TYPE RS38L-BASXML_ENABLED,
             IMPORT            TYPE STANDARD TABLE OF RSIMP WITH DEFAULT KEY,
             CHANGING          TYPE STANDARD TABLE OF RSCHA WITH DEFAULT KEY,
             EXPORT            TYPE STANDARD TABLE OF RSEXP WITH DEFAULT KEY,
             TABLES            TYPE STANDARD TABLE OF RSTBL WITH DEFAULT KEY,
             EXCEPTION         TYPE STANDARD TABLE OF RSEXC WITH DEFAULT KEY,
             DOCUMENTATION     TYPE STANDARD TABLE OF RSFDO WITH DEFAULT KEY,
             EXCEPTION_CLASSES TYPE ABAP_BOOL,
           END OF TY_FUNCTION.

    TYPES: TY_FUNCTION_TT TYPE STANDARD TABLE OF TY_FUNCTION WITH DEFAULT KEY.

    METHODS MAIN_NAME
      RETURNING VALUE(RV_PROGRAM) TYPE PROGRAM
      RAISING   LCX_EXCEPTION.

    METHODS FUNCTIONS
      RETURNING VALUE(RT_FUNCTAB) TYPE TY_RS38L_INCL_TT
      RAISING   LCX_EXCEPTION.

    METHODS INCLUDES
      RETURNING VALUE(RT_INCLUDES) TYPE RSO_T_OBJNM
      RAISING   LCX_EXCEPTION.

    METHODS SERIALIZE_FUNCTIONS
      RETURNING VALUE(RT_FUNCTIONS) TYPE TY_FUNCTION_TT
      RAISING   LCX_EXCEPTION.

    METHODS DESERIALIZE_FUNCTIONS
      IMPORTING IT_FUNCTIONS TYPE TY_FUNCTION_TT
      RAISING   LCX_EXCEPTION.

    METHODS SERIALIZE_XML
      IMPORTING IO_XML TYPE REF TO LCL_XML_OUTPUT
      RAISING   LCX_EXCEPTION.

    METHODS DESERIALIZE_XML
      IMPORTING IO_XML     TYPE REF TO LCL_XML_INPUT
                IV_PACKAGE TYPE DEVCLASS
      RAISING   LCX_EXCEPTION.

    METHODS SERIALIZE_INCLUDES
      RAISING LCX_EXCEPTION.

    METHODS DESERIALIZE_INCLUDES
      IMPORTING IO_XML     TYPE REF TO LCL_XML_INPUT
                IV_PACKAGE TYPE DEVCLASS
      RAISING   LCX_EXCEPTION.

    METHODS ARE_EXCEPTIONS_CLASS_BASED
      IMPORTING IV_FUNCTION_NAME TYPE RS38L_FNAM
      RETURNING VALUE(RV_RETURN) TYPE ABAP_BOOL
      RAISING   LCX_EXCEPTION.

ENDCLASS.                    "lcl_object_fugr DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_object_dtel IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS LCL_OBJECT_FUGR IMPLEMENTATION.

  METHOD LIF_OBJECT~HAS_CHANGED_SINCE.

    DATA: LT_FUNCTAB  TYPE TY_RS38L_INCL_TT,
          LT_INCLUDES TYPE RSO_T_OBJNM.

    FIELD-SYMBOLS: <LS_FUNC>      LIKE LINE OF LT_FUNCTAB,
                   <INCLUDE_NAME> LIKE LINE OF LT_INCLUDES.

    LT_INCLUDES = INCLUDES( ). " Main prog also included here

    LOOP AT LT_INCLUDES ASSIGNING <INCLUDE_NAME>.
      RV_CHANGED = CHECK_PROG_CHANGED_SINCE(
        IV_PROGRAM   = <INCLUDE_NAME>
        IV_TIMESTAMP = IV_TIMESTAMP ).
      IF RV_CHANGED = ABAP_TRUE.
        RETURN.
      ENDIF.
    ENDLOOP.

    LT_FUNCTAB = FUNCTIONS( ).

    LOOP AT LT_FUNCTAB ASSIGNING <LS_FUNC>.
      RV_CHANGED = CHECK_PROG_CHANGED_SINCE(
        IV_PROGRAM   = <LS_FUNC>-INCLUDE
        IV_TIMESTAMP = IV_TIMESTAMP ).
      IF RV_CHANGED = ABAP_TRUE.
        RETURN.
      ENDIF.
    ENDLOOP.

  ENDMETHOD.  "lif_object~has_changed_since

  METHOD LIF_OBJECT~CHANGED_BY.

    TYPES: BEGIN OF TY_STAMPS,
             USER TYPE XUBNAME,
             DATE TYPE D,
             TIME TYPE T,
           END OF TY_STAMPS.

    DATA: LT_STAMPS   TYPE STANDARD TABLE OF TY_STAMPS WITH DEFAULT KEY,
          LV_PROGRAM  TYPE PROGRAM,
          LT_INCLUDES TYPE RSO_T_OBJNM.

    FIELD-SYMBOLS: <LS_STAMP>   LIKE LINE OF LT_STAMPS,
                   <LV_INCLUDE> LIKE LINE OF LT_INCLUDES.


    LV_PROGRAM = MAIN_NAME( ).

    CALL FUNCTION 'RS_GET_ALL_INCLUDES'
      EXPORTING
        PROGRAM      = LV_PROGRAM
      TABLES
        INCLUDETAB   = LT_INCLUDES
      EXCEPTIONS
        NOT_EXISTENT = 1
        NO_PROGRAM   = 2
        OTHERS       = 3.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'Error from RS_GET_ALL_INCLUDES' ).
    ENDIF.

    SELECT UNAM AS USER UDAT AS DATE UTIME AS TIME FROM REPOSRC
      APPENDING CORRESPONDING FIELDS OF TABLE LT_STAMPS
      WHERE PROGNAME = LV_PROGRAM
      AND   R3STATE = 'A'.                                "#EC CI_SUBRC

    LOOP AT LT_INCLUDES ASSIGNING <LV_INCLUDE>.
      SELECT UNAM AS USER UDAT AS DATE UTIME AS TIME FROM REPOSRC
        APPENDING CORRESPONDING FIELDS OF TABLE LT_STAMPS
        WHERE PROGNAME = <LV_INCLUDE>
        AND   R3STATE = 'A'.                              "#EC CI_SUBRC
    ENDLOOP.

    SELECT UNAM AS USER UDAT AS DATE UTIME AS TIME FROM REPOTEXT " Program text pool
      APPENDING CORRESPONDING FIELDS OF TABLE LT_STAMPS
      WHERE PROGNAME = LV_PROGRAM
      AND   R3STATE = 'A'.                                "#EC CI_SUBRC

    SELECT VAUTOR AS USER VDATUM AS DATE VZEIT AS TIME FROM EUDB         " GUI
      APPENDING CORRESPONDING FIELDS OF TABLE LT_STAMPS
      WHERE RELID = 'CU'
      AND   NAME  = LV_PROGRAM
      AND   SRTF2 = 0 ##TOO_MANY_ITAB_FIELDS.

* Screens: username not stored in D020S database table

    SORT LT_STAMPS BY DATE DESCENDING TIME DESCENDING.

    READ TABLE LT_STAMPS INDEX 1 ASSIGNING <LS_STAMP>.
    IF SY-SUBRC = 0.
      RV_USER = <LS_STAMP>-USER.
    ELSE.
      RV_USER = C_USER_UNKNOWN.
    ENDIF.

  ENDMETHOD.

  METHOD LIF_OBJECT~GET_METADATA.
    RS_METADATA = GET_METADATA( ).
  ENDMETHOD.                    "lif_object~get_metadata

  METHOD LIF_OBJECT~EXISTS.

    DATA: LV_POOL  TYPE TLIBG-AREA.


    LV_POOL = MS_ITEM-OBJ_NAME.
    CALL FUNCTION 'RS_FUNCTION_POOL_EXISTS'
      EXPORTING
        FUNCTION_POOL   = LV_POOL
      EXCEPTIONS
        POOL_NOT_EXISTS = 1.
    RV_BOOL = BOOLC( SY-SUBRC <> 1 ).

  ENDMETHOD.                    "lif_object~exists

  METHOD DESERIALIZE_FUNCTIONS.

    DATA: LV_INCLUDE   TYPE RS38L-INCLUDE,
          LV_AREA      TYPE RS38L-AREA,
          LV_GROUP     TYPE RS38L-AREA,
          LV_NAMESPACE TYPE RS38L-NAMESPACE,
          LT_SOURCE    TYPE TABLE OF ABAPTXT255,
          LV_DUMMY     TYPE STRING.

    FIELD-SYMBOLS: <LS_FUNC> LIKE LINE OF IT_FUNCTIONS.

    LOOP AT IT_FUNCTIONS ASSIGNING <LS_FUNC>.

      LT_SOURCE = MO_FILES->READ_ABAP( IV_EXTRA = <LS_FUNC>-FUNCNAME ).

      LV_AREA = MS_ITEM-OBJ_NAME.

      CALL FUNCTION 'FUNCTION_INCLUDE_SPLIT'
        EXPORTING
          COMPLETE_AREA                = LV_AREA
        IMPORTING
          NAMESPACE                    = LV_NAMESPACE
          GROUP                        = LV_GROUP
        EXCEPTIONS
          OTHERS                       = 12.

      IF SY-SUBRC <> 0.
        LCX_EXCEPTION=>RAISE( 'error from FUNCTION_INCLUDE_SPLIT' ).
      ENDIF.

      CALL FUNCTION 'FUNCTION_EXISTS'
        EXPORTING
          FUNCNAME           = <LS_FUNC>-FUNCNAME
        IMPORTING
          INCLUDE            = LV_INCLUDE
        EXCEPTIONS
          FUNCTION_NOT_EXIST = 1.
      IF SY-SUBRC = 0.
* delete the function module to make sure the parameters are updated
* havent found a nice way to update the paramters
        CALL FUNCTION 'FUNCTION_DELETE'
          EXPORTING
            FUNCNAME                 = <LS_FUNC>-FUNCNAME
            SUPPRESS_SUCCESS_MESSAGE = ABAP_TRUE
          EXCEPTIONS
            ERROR_MESSAGE            = 1
            OTHERS                   = 2.
        IF SY-SUBRC <> 0.
          LCX_EXCEPTION=>RAISE( 'error from FUNCTION_DELETE' ).
        ENDIF.
      ENDIF.

      CALL FUNCTION 'RS_FUNCTIONMODULE_INSERT'
        EXPORTING
          FUNCNAME                = <LS_FUNC>-FUNCNAME
          FUNCTION_POOL           = LV_GROUP
          INTERFACE_GLOBAL        = <LS_FUNC>-GLOBAL_FLAG
          REMOTE_CALL             = <LS_FUNC>-REMOTE_CALL
          SHORT_TEXT              = <LS_FUNC>-SHORT_TEXT
          UPDATE_TASK             = <LS_FUNC>-UPDATE_TASK
          EXCEPTION_CLASS         = <LS_FUNC>-EXCEPTION_CLASSES
          NAMESPACE               = LV_NAMESPACE
          REMOTE_BASXML_SUPPORTED = <LS_FUNC>-REMOTE_BASXML
        IMPORTING
          FUNCTION_INCLUDE        = LV_INCLUDE
        TABLES
          IMPORT_PARAMETER        = <LS_FUNC>-IMPORT
          EXPORT_PARAMETER        = <LS_FUNC>-EXPORT
          TABLES_PARAMETER        = <LS_FUNC>-TABLES
          CHANGING_PARAMETER      = <LS_FUNC>-CHANGING
          EXCEPTION_LIST          = <LS_FUNC>-EXCEPTION
          PARAMETER_DOCU          = <LS_FUNC>-DOCUMENTATION
        EXCEPTIONS
          DOUBLE_TASK             = 1
          ERROR_MESSAGE           = 2
          FUNCTION_ALREADY_EXISTS = 3
          INVALID_FUNCTION_POOL   = 4
          INVALID_NAME            = 5
          TOO_MANY_FUNCTIONS      = 6
          NO_MODIFY_PERMISSION    = 7
          NO_SHOW_PERMISSION      = 8
          ENQUEUE_SYSTEM_FAILURE  = 9
          CANCELED_IN_CORR        = 10
          OTHERS                  = 11.
      IF SY-SUBRC <> 0.
        LCX_EXCEPTION=>RAISE( |error from RS_FUNCTIONMODULE_INSERT: {
          SY-SUBRC } { SY-MSGID }{ SY-MSGNO }| ).
      ENDIF.

      INSERT REPORT LV_INCLUDE FROM LT_SOURCE.
    ENDLOOP.

  ENDMETHOD.                    "deserialize_functions

  METHOD DESERIALIZE_INCLUDES.

    DATA: LO_XML       TYPE REF TO LCL_XML_INPUT,
          LS_PROGDIR   TYPE TY_PROGDIR,
          LT_INCLUDES  TYPE RSO_T_OBJNM,
          LT_TPOOL     TYPE TEXTPOOL_TABLE,
          LT_TPOOL_EXT TYPE LIF_DEFS=>TY_TPOOL_TT,
          LT_SOURCE    TYPE TABLE OF ABAPTXT255.

    FIELD-SYMBOLS: <LV_INCLUDE> LIKE LINE OF LT_INCLUDES.


    TADIR_INSERT( IV_PACKAGE ).

    IO_XML->READ( EXPORTING IV_NAME = 'INCLUDES'
                  CHANGING CG_DATA = LT_INCLUDES ).

    LOOP AT LT_INCLUDES ASSIGNING <LV_INCLUDE>.

      LT_SOURCE = MO_FILES->READ_ABAP( IV_EXTRA = <LV_INCLUDE> ).

      LO_XML = MO_FILES->READ_XML( <LV_INCLUDE> ).

      LO_XML->READ( EXPORTING IV_NAME = 'PROGDIR'
                    CHANGING CG_DATA = LS_PROGDIR ).

      LO_XML->READ( EXPORTING IV_NAME = 'TPOOL'
                    CHANGING CG_DATA = LT_TPOOL_EXT ).
      LT_TPOOL = READ_TPOOL( LT_TPOOL_EXT ).

      DESERIALIZE_PROGRAM( IS_PROGDIR = LS_PROGDIR
                           IT_SOURCE  = LT_SOURCE
                           IT_TPOOL   = LT_TPOOL
                           IV_PACKAGE = IV_PACKAGE ).

      DESERIALIZE_TEXTPOOL( IV_PROGRAM = <LV_INCLUDE>
                            IT_TPOOL   = LT_TPOOL ).

    ENDLOOP.

  ENDMETHOD.                    "deserialize_includes

  METHOD DESERIALIZE_XML.

    DATA: LV_COMPLETE  TYPE RS38L-AREA,
          LV_NAMESPACE TYPE RS38L-NAMESPACE,
          LV_AREAT     TYPE TLIBT-AREAT,
          LV_STEXT     TYPE TFTIT-STEXT,
          LV_GROUP     TYPE RS38L-AREA.


    LV_COMPLETE = MS_ITEM-OBJ_NAME.

    CALL FUNCTION 'FUNCTION_INCLUDE_SPLIT'
      EXPORTING
        COMPLETE_AREA                = LV_COMPLETE
      IMPORTING
        NAMESPACE                    = LV_NAMESPACE
        GROUP                        = LV_GROUP
      EXCEPTIONS
        INCLUDE_NOT_EXISTS           = 1
        GROUP_NOT_EXISTS             = 2
        NO_SELECTIONS                = 3
        NO_FUNCTION_INCLUDE          = 4
        NO_FUNCTION_POOL             = 5
        DELIMITER_WRONG_POSITION     = 6
        NO_CUSTOMER_FUNCTION_GROUP   = 7
        NO_CUSTOMER_FUNCTION_INCLUDE = 8
        RESERVED_NAME_CUSTOMER       = 9
        NAMESPACE_TOO_LONG           = 10
        AREA_LENGTH_ERROR            = 11
        OTHERS                       = 12.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'error from FUNCTION_INCLUDE_SPLIT' ).
    ENDIF.

    IO_XML->READ( EXPORTING IV_NAME = 'AREAT'
                  CHANGING CG_DATA = LV_AREAT ).
    LV_STEXT = LV_AREAT.

    CALL FUNCTION 'RS_FUNCTION_POOL_INSERT'
      EXPORTING
        FUNCTION_POOL           = LV_GROUP
        SHORT_TEXT              = LV_STEXT
        NAMESPACE               = LV_NAMESPACE
        DEVCLASS                = IV_PACKAGE
      EXCEPTIONS
        NAME_ALREADY_EXISTS     = 1
        NAME_NOT_CORRECT        = 2
        FUNCTION_ALREADY_EXISTS = 3
        INVALID_FUNCTION_POOL   = 4
        INVALID_NAME            = 5
        TOO_MANY_FUNCTIONS      = 6
        NO_MODIFY_PERMISSION    = 7
        NO_SHOW_PERMISSION      = 8
        ENQUEUE_SYSTEM_FAILURE  = 9
        CANCELED_IN_CORR        = 10
        UNDEFINED_ERROR         = 11
        OTHERS                  = 12.
    IF SY-SUBRC <> 0 AND SY-SUBRC <> 1 AND SY-SUBRC <> 3.
* todo, change description
      LCX_EXCEPTION=>RAISE( 'error from RS_FUNCTION_POOL_INSERT' ).
    ENDIF.

  ENDMETHOD.                    "deserialize_xml

  METHOD SERIALIZE_XML.

    DATA: LT_INCLUDES TYPE RSO_T_OBJNM,
          LV_AREAT    TYPE TLIBT-AREAT.


    SELECT SINGLE AREAT INTO LV_AREAT
      FROM TLIBT
      WHERE SPRAS = MV_LANGUAGE
      AND AREA = MS_ITEM-OBJ_NAME.        "#EC CI_GENBUFF "#EC CI_SUBRC

    LT_INCLUDES = INCLUDES( ).

    IO_XML->ADD( IV_NAME = 'AREAT'
                 IG_DATA = LV_AREAT ).
    IO_XML->ADD( IV_NAME = 'INCLUDES'
                 IG_DATA = LT_INCLUDES ).

  ENDMETHOD.                    "serialize_xml

  METHOD INCLUDES.

    TYPES: BEGIN OF TY_REPOSRC,
             PROGNAME TYPE REPOSRC-PROGNAME,
             CNAM     TYPE REPOSRC-CNAM,
           END OF TY_REPOSRC.

    DATA: LT_REPOSRC   TYPE STANDARD TABLE OF TY_REPOSRC WITH DEFAULT KEY,
          LS_REPOSRC   LIKE LINE OF LT_REPOSRC,
          LV_PROGRAM   TYPE PROGRAM,
          LV_OFFSET_NS TYPE I,
          LV_TABIX     LIKE SY-TABIX,
          LT_FUNCTAB   TYPE TY_RS38L_INCL_TT.

    FIELD-SYMBOLS: <LV_INCLUDE> LIKE LINE OF RT_INCLUDES,
                   <LS_FUNC>    LIKE LINE OF LT_FUNCTAB.


    LV_PROGRAM = MAIN_NAME( ).
    LT_FUNCTAB = FUNCTIONS( ).

    CALL FUNCTION 'RS_GET_ALL_INCLUDES'
      EXPORTING
        PROGRAM      = LV_PROGRAM
*       WITH_RESERVED_INCLUDES =
*       WITH_CLASS_INCLUDES    = ' ' hmm, todo
      TABLES
        INCLUDETAB   = RT_INCLUDES
      EXCEPTIONS
        NOT_EXISTENT = 1
        NO_PROGRAM   = 2
        OTHERS       = 3.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'Error from RS_GET_ALL_INCLUDES' ).
    ENDIF.

    LOOP AT LT_FUNCTAB ASSIGNING <LS_FUNC>.
      DELETE TABLE RT_INCLUDES FROM <LS_FUNC>-INCLUDE.
    ENDLOOP.

* handle generated maintenance views
    APPEND INITIAL LINE TO RT_INCLUDES ASSIGNING <LV_INCLUDE>.
    IF MS_ITEM-OBJ_NAME(1) <> '/'.
      "FGroup name does not contain a namespace
      <LV_INCLUDE> = |L{ MS_ITEM-OBJ_NAME }T00|.
    ELSE.
      "FGroup name contains a namespace
      LV_OFFSET_NS = FIND( VAL = MS_ITEM-OBJ_NAME+1 SUB = '/' ).
      LV_OFFSET_NS = LV_OFFSET_NS + 2.
      <LV_INCLUDE> = |{ MS_ITEM-OBJ_NAME(LV_OFFSET_NS) }L{ MS_ITEM-OBJ_NAME+LV_OFFSET_NS }T00|.
    ENDIF.

    IF LINES( RT_INCLUDES ) > 0.
      SELECT PROGNAME CNAM FROM REPOSRC
        INTO TABLE LT_REPOSRC
        FOR ALL ENTRIES IN RT_INCLUDES
        WHERE PROGNAME = RT_INCLUDES-TABLE_LINE
        AND R3STATE = 'A'.
      SORT LT_REPOSRC BY PROGNAME ASCENDING.
    ENDIF.

    LOOP AT RT_INCLUDES ASSIGNING <LV_INCLUDE>.
      LV_TABIX = SY-TABIX.

* skip SAP standard includes and also make sure the include exists
      READ TABLE LT_REPOSRC INTO LS_REPOSRC
        WITH KEY PROGNAME = <LV_INCLUDE> BINARY SEARCH.
      IF SY-SUBRC <> 0 OR LS_REPOSRC-CNAM = 'SAP'.
        DELETE RT_INCLUDES INDEX LV_TABIX.
      ENDIF.

    ENDLOOP.

    APPEND LV_PROGRAM TO RT_INCLUDES.

  ENDMETHOD.                    "includes

  METHOD FUNCTIONS.

    DATA: LV_AREA TYPE RS38L-AREA.


    LV_AREA = MS_ITEM-OBJ_NAME.

    CALL FUNCTION 'RS_FUNCTION_POOL_CONTENTS'
      EXPORTING
        FUNCTION_POOL           = LV_AREA
      TABLES
        FUNCTAB                 = RT_FUNCTAB
      EXCEPTIONS
        FUNCTION_POOL_NOT_FOUND = 1
        OTHERS                  = 2.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'Error from RS_FUNCTION_POOL_CONTENTS' ).
    ENDIF.

    SORT RT_FUNCTAB BY FUNCNAME ASCENDING.
    DELETE ADJACENT DUPLICATES FROM RT_FUNCTAB COMPARING FUNCNAME.

  ENDMETHOD.                    "functions

  METHOD MAIN_NAME.

    DATA: LV_AREA      TYPE RS38L-AREA,
          LV_NAMESPACE TYPE RS38L-NAMESPACE,
          LV_GROUP     TYPE RS38L-AREA.


    LV_AREA = MS_ITEM-OBJ_NAME.

    CALL FUNCTION 'FUNCTION_INCLUDE_SPLIT'
      EXPORTING
        COMPLETE_AREA                = LV_AREA
      IMPORTING
        NAMESPACE                    = LV_NAMESPACE
        GROUP                        = LV_GROUP
      EXCEPTIONS
        INCLUDE_NOT_EXISTS           = 1
        GROUP_NOT_EXISTS             = 2
        NO_SELECTIONS                = 3
        NO_FUNCTION_INCLUDE          = 4
        NO_FUNCTION_POOL             = 5
        DELIMITER_WRONG_POSITION     = 6
        NO_CUSTOMER_FUNCTION_GROUP   = 7
        NO_CUSTOMER_FUNCTION_INCLUDE = 8
        RESERVED_NAME_CUSTOMER       = 9
        NAMESPACE_TOO_LONG           = 10
        AREA_LENGTH_ERROR            = 11
        OTHERS                       = 12.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'Error from FUNCTION_INCLUDE_SPLIT' ).
    ENDIF.

    CONCATENATE LV_NAMESPACE 'SAPL' LV_GROUP INTO RV_PROGRAM.

  ENDMETHOD.                    "main_name

  METHOD SERIALIZE_FUNCTIONS.

    DATA:
      LT_SOURCE     TYPE TABLE OF RSSOURCE,
      LT_FUNCTAB    TYPE TY_RS38L_INCL_TT,
      LT_NEW_SOURCE TYPE RSFB_SOURCE,
      LS_FUNCTION   LIKE LINE OF RT_FUNCTIONS.

    FIELD-SYMBOLS: <LS_FUNC> LIKE LINE OF LT_FUNCTAB.


    LT_FUNCTAB = FUNCTIONS( ).

    LOOP AT LT_FUNCTAB ASSIGNING <LS_FUNC>.
* fm RPY_FUNCTIONMODULE_READ does not support source code
* lines longer than 72 characters
      CLEAR LS_FUNCTION.
      MOVE-CORRESPONDING <LS_FUNC> TO LS_FUNCTION.

      CLEAR LT_NEW_SOURCE.
      CLEAR LT_SOURCE.

      CALL FUNCTION 'RPY_FUNCTIONMODULE_READ_NEW'
        EXPORTING
          FUNCTIONNAME            = <LS_FUNC>-FUNCNAME
        IMPORTING
          GLOBAL_FLAG             = LS_FUNCTION-GLOBAL_FLAG
          REMOTE_CALL             = LS_FUNCTION-REMOTE_CALL
          UPDATE_TASK             = LS_FUNCTION-UPDATE_TASK
          SHORT_TEXT              = LS_FUNCTION-SHORT_TEXT
          REMOTE_BASXML_SUPPORTED = LS_FUNCTION-REMOTE_BASXML
        TABLES
          IMPORT_PARAMETER        = LS_FUNCTION-IMPORT
          CHANGING_PARAMETER      = LS_FUNCTION-CHANGING
          EXPORT_PARAMETER        = LS_FUNCTION-EXPORT
          TABLES_PARAMETER        = LS_FUNCTION-TABLES
          EXCEPTION_LIST          = LS_FUNCTION-EXCEPTION
          DOCUMENTATION           = LS_FUNCTION-DOCUMENTATION
          SOURCE                  = LT_SOURCE
        CHANGING
          NEW_SOURCE              = LT_NEW_SOURCE
        EXCEPTIONS
          ERROR_MESSAGE           = 1
          FUNCTION_NOT_FOUND      = 2
          INVALID_NAME            = 3
          OTHERS                  = 4.
      IF SY-SUBRC = 2.
        CONTINUE.
      ELSEIF SY-SUBRC <> 0.
        LCX_EXCEPTION=>RAISE( 'Error from RPY_FUNCTIONMODULE_READ_NEW' ).
      ENDIF.

      LS_FUNCTION-EXCEPTION_CLASSES = ARE_EXCEPTIONS_CLASS_BASED( <LS_FUNC>-FUNCNAME ).

      APPEND LS_FUNCTION TO RT_FUNCTIONS.

      IF NOT LT_NEW_SOURCE IS INITIAL.
        MO_FILES->ADD_ABAP( IV_EXTRA = <LS_FUNC>-FUNCNAME
                            IT_ABAP  = LT_NEW_SOURCE ).
      ELSE.
        MO_FILES->ADD_ABAP( IV_EXTRA = <LS_FUNC>-FUNCNAME
                            IT_ABAP  = LT_SOURCE ).
      ENDIF.

    ENDLOOP.

  ENDMETHOD.                    "serialize_functions

  METHOD SERIALIZE_INCLUDES.

    DATA: LT_INCLUDES TYPE RSO_T_OBJNM.

    FIELD-SYMBOLS: <LV_INCLUDE> LIKE LINE OF LT_INCLUDES.


    LT_INCLUDES = INCLUDES( ).

    LOOP AT LT_INCLUDES ASSIGNING <LV_INCLUDE>.

* todo, filename is not correct, a include can be used in several programs
      SERIALIZE_PROGRAM( IS_ITEM    = MS_ITEM
                         IO_FILES   = MO_FILES
                         IV_PROGRAM = <LV_INCLUDE>
                         IV_EXTRA   = <LV_INCLUDE> ).

    ENDLOOP.

  ENDMETHOD.                    "serialize_includes

  METHOD ARE_EXCEPTIONS_CLASS_BASED.
    DATA:
      LT_DOKUMENTATION    TYPE TABLE OF FUNCT,
      LT_EXCEPTION_LIST   TYPE TABLE OF RSEXC,
      LT_EXPORT_PARAMETER TYPE TABLE OF RSEXP,
      LT_IMPORT_PARAMETER TYPE TABLE OF RSIMP,
      LT_TABLES_PARAMETER TYPE TABLE OF RSTBL.

    CALL FUNCTION 'FUNCTION_IMPORT_DOKU'
      EXPORTING
        FUNCNAME           = IV_FUNCTION_NAME
      IMPORTING
        EXCEPTION_CLASS    = RV_RETURN
      TABLES
        DOKUMENTATION      = LT_DOKUMENTATION
        EXCEPTION_LIST     = LT_EXCEPTION_LIST
        EXPORT_PARAMETER   = LT_EXPORT_PARAMETER
        IMPORT_PARAMETER   = LT_IMPORT_PARAMETER
        TABLES_PARAMETER   = LT_TABLES_PARAMETER
      EXCEPTIONS
        ERROR_MESSAGE      = 1
        FUNCTION_NOT_FOUND = 2
        INVALID_NAME       = 3
        OTHERS             = 4.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'Error from FUNCTION_IMPORT_DOKU' ).
    ENDIF.
  ENDMETHOD.

  METHOD LIF_OBJECT~SERIALIZE.

* function group SEUF
* function group SIFP
* function group SUNI

    DATA: LT_FUNCTIONS    TYPE TY_FUNCTION_TT,
          LS_PROGDIR      TYPE TY_PROGDIR,
          LV_PROGRAM_NAME TYPE PROGRAMM,
          LT_DYNPROS      TYPE TY_DYNPRO_TT,
          LS_CUA          TYPE TY_CUA.

    IF LIF_OBJECT~EXISTS( ) = ABAP_FALSE.
      RETURN.
    ENDIF.

    SERIALIZE_XML( IO_XML ).

    LT_FUNCTIONS = SERIALIZE_FUNCTIONS( ).
    IO_XML->ADD( IV_NAME = 'FUNCTIONS'
                 IG_DATA = LT_FUNCTIONS ).

    SERIALIZE_INCLUDES( ).

    LV_PROGRAM_NAME = MAIN_NAME( ).
    LS_PROGDIR = READ_PROGDIR( LV_PROGRAM_NAME ).

    IF LS_PROGDIR-SUBC = 'F'.
      LT_DYNPROS = SERIALIZE_DYNPROS( LV_PROGRAM_NAME ).
      IO_XML->ADD( IV_NAME = 'DYNPROS'
                   IG_DATA = LT_DYNPROS ).

      LS_CUA = SERIALIZE_CUA( LV_PROGRAM_NAME ).
      IO_XML->ADD( IV_NAME = 'CUA'
                   IG_DATA = LS_CUA ).
    ENDIF.

  ENDMETHOD.                    "serialize

  METHOD LIF_OBJECT~DESERIALIZE.

    DATA: LV_PROGRAM_NAME TYPE PROGRAMM,
          LT_FUNCTIONS    TYPE TY_FUNCTION_TT,
          LT_DYNPROS      TYPE TY_DYNPRO_TT,
          LS_CUA          TYPE TY_CUA.


    DESERIALIZE_XML(
      IO_XML     = IO_XML
      IV_PACKAGE = IV_PACKAGE ).

    IO_XML->READ( EXPORTING IV_NAME = 'FUNCTIONS'
                  CHANGING CG_DATA = LT_FUNCTIONS ).
    DESERIALIZE_FUNCTIONS( LT_FUNCTIONS ).

    DESERIALIZE_INCLUDES(
      IO_XML     = IO_XML
      IV_PACKAGE = IV_PACKAGE ).

    LV_PROGRAM_NAME = MAIN_NAME( ).

    IO_XML->READ( EXPORTING IV_NAME = 'DYNPROS'
                  CHANGING CG_DATA = LT_DYNPROS ).
    DESERIALIZE_DYNPROS( IT_DYNPROS = LT_DYNPROS ).

    IO_XML->READ( EXPORTING IV_NAME = 'CUA'
                  CHANGING CG_DATA = LS_CUA ).
    DESERIALIZE_CUA( IV_PROGRAM_NAME = LV_PROGRAM_NAME
                     IS_CUA = LS_CUA ).

  ENDMETHOD.                    "deserialize

  METHOD LIF_OBJECT~DELETE.

    DATA: LV_AREA TYPE RS38L-AREA.


    LV_AREA = MS_ITEM-OBJ_NAME.

    CALL FUNCTION 'RS_FUNCTION_POOL_DELETE'
      EXPORTING
        AREA                   = LV_AREA
        SUPPRESS_POPUPS        = ABAP_TRUE
        SKIP_PROGRESS_IND      = ABAP_TRUE
      EXCEPTIONS
        CANCELED_IN_CORR       = 1
        ENQUEUE_SYSTEM_FAILURE = 2
        FUNCTION_EXIST         = 3
        NOT_EXECUTED           = 4
        NO_MODIFY_PERMISSION   = 5
        NO_SHOW_PERMISSION     = 6
        PERMISSION_FAILURE     = 7
        POOL_NOT_EXIST         = 8
        CANCELLED              = 9
        OTHERS                 = 10.
    IF SY-SUBRC <> 0.
      LCX_EXCEPTION=>RAISE( 'error from RS_FUNCTION_POOL_DELETE' ).
    ENDIF.

  ENDMETHOD.                    "delete

  METHOD LIF_OBJECT~JUMP.

    CALL FUNCTION 'RS_TOOL_ACCESS'
      EXPORTING
        OPERATION     = 'SHOW'
        OBJECT_NAME   = MS_ITEM-OBJ_NAME
        OBJECT_TYPE   = 'FUGR'
        IN_NEW_WINDOW = ABAP_TRUE.

  ENDMETHOD.                    "jump

  METHOD LIF_OBJECT~COMPARE_TO_REMOTE_VERSION.
    CREATE OBJECT RO_COMPARISON_RESULT TYPE LCL_COMPARISON_NULL.
  ENDMETHOD.

ENDCLASS.                    "lcl_object_fugr IMPLEMENTATION
